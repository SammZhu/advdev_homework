#!groovy
podTemplate(
  label: "skopeo-pod",
  cloud: "openshift",
  inheritFrom: "maven",
  containers: [
    containerTemplate(
      name: "jnlp",
      image: "docker-registry.default.svc:5000/${GUID}-jenkins/jenkins-agent-appdev",
      resourceRequestMemory: "1Gi",
      resourceLimitMemory: "2Gi",
      resourceRequestCpu: "1",
      resourceLimitCpu: "2"
    )
  ]
) {
  node('skopeo-pod') {
    echo "GUID: ${GUID}"
    echo "CLUSTER: ${CLUSTER}"    
    // Define Maven Command to point to the correct
    // settings for our Nexus installation
    def mvnCmd = "mvn -s ../nexus_settings.xml"

    // Checkout Source Code.
    stage('Checkout Source') {
      echo "Checking out Source Code"
      git url: 'https://github.com/SammZhu/advdev_homework'
      checkout scm
    }

    // Build the Tasks Service
    dir('openshift-tasks') {
      // The following variables need to be defined at the top level
      // and not inside the scope of a stage - otherwise they would not
      // be accessible from other stages.
      // Extract version from the pom.xml
      def version = getVersionFromPom("pom.xml")

      // TBD Set the tag for the development image: version + build number
      def devTag  = "${version}-${BUILD_NUMBER}"
      // Set the tag for the production image: version
      def prodTag = "${version}"

      // Using Maven build the war file
      // Do not run tests in this step
      stage('Build war') {
        echo "Building version ${devTag}"

        sh "${mvnCmd} clean package -DskipTests=true"
      }

      stage("Do two things in parallel") {
        failFast true
        parallel {
          // Using Maven run the unit tests
          stage('Unit Tests') {
            echo "Running Unit Tests"
    
            sh "${mvnCmd} test"
          }

          // Using Maven to call SonarQube for Code Analysis
          stage('Code Analysis') {
            echo "Running Code Analysis"
    
            sh "${mvnCmd} sonar:sonar -Dsonar.host.url=http://sonarqube.gpte-hw-cicd.svc.cluster.local:9000 -Dsonar.projectName=${JOB_BASE_NAME}-${devTag}"
          }
        }
      }

      // Publish the built war file to Nexus
      stage('Publish to Nexus') {
        echo "Publish to Nexus"

        sh "${mvnCmd} deploy -DskipTests=true -DaltDeploymentRepository=nexus::default::http://nexus3.gpte-hw-cicd.svc.cluster.local:8081/repository/maven-all-public"
      }

      // Build the OpenShift Image in OpenShift and tag it.
      stage('Build and Tag OpenShift Image') {
        echo "Building OpenShift container image tasks:${devTag}"

        // Start Binary Build in OpenShift using the file we just published
        sh "oc start-build openshift-tasks --follow --from-file=http://nexus3.gpte-hw-cicd.svc.cluster.local:8081/repository/maven-all-public/org/jboss/quickstarts/eap/openshift-tasks/${version}/tasks-${version}.war -n ${GUID}-tasks-dev"
        // Tag the image using the devTag
        openshiftTag alias: 'false', destStream: 'tasks', destTag: devTag, destinationNamespace: "${GUID}-tasks-dev", namespace: "${GUID}-tasks-dev", srcStream: 'tasks', srcTag: 'latest', verbose: 'false'
      }

      // Deploy the built image to the Development Environment.
      stage('Deploy to Dev') {
        echo "Deploying container image to Development Project"

        // TBD: Deploy to development Project
        //      Set Image, Set VERSION
        //      Make sure the application is running and ready before proceeding
        // Update the Image on the Development Deployment Config
        sh "oc set image dc/tasks tasks=docker-registry.default.svc:5000/${GUID}-tasks-dev/tasks:${devTag} -n ${GUID}-tasks-dev"

        // Update the Config Map which contains the config for the mlbparks application
        sh "oc delete configmap openshift-tasks-config -n ${GUID}-tasks-dev --ignore-not-found=true"

        sh "oc create configmap openshift-tasks-config \
                  --from-literal=APPNAME='openshift-tasks (Dev)' --from-literal=VERSION='${devTag} (tasks-dev)' -n ${GUID}-tasks-dev"

        sh "oc set env dc/tasks --from=configmap/openshift-tasks-config -n ${GUID}-tasks-dev" 

        // Deploy the development application
        openshiftDeploy depCfg: 'tasks', namespace: "${GUID}-tasks-dev", verbose: 'false', waitTime: '', waitUnit: 'sec'
        openshiftVerifyDeployment depCfg: 'tasks', namespace: "${GUID}-tasks-dev", replicaCount: '1', verbose: 'false', verifyReplicaCount: 'false', waitTime: '', waitUnit: 'sec'
        openshiftVerifyService namespace: "${GUID}-tasks-dev", svcName: 'openshift-tasks', verbose: 'false'
      }

      // Copy Image to Nexus container registry
      stage('Copy Image to Nexus container registry') {
        echo "Copy image to Nexus container registry"

        sh "skopeo copy --src-tls-verify=false --dest-tls-verify=false --src-creds openshift:\$(oc whoami -t) --dest-creds admin:redhat docker://docker-registry.default.svc.cluster.local:5000/${GUID}-tasks-dev/tasks:${devTag} docker://nexus3-registry.gpte-hw-cicd.svc.cluster.local:5000/tasks:${devTag}"

        // Tag the built image with the production tag
        openshiftTag alias: 'false', destStream: 'tasks', destTag: prodTag, destinationNamespace: "${GUID}-tasks-dev", namespace: "${GUID}-tasks-dev", srcStream: 'tasks', srcTag: devTag, verbose: 'false'
      }

      // Blue/Green Deployment into Production
      // -------------------------------------
      def destApp   = "tasks-green"
      def destcfgMap = "Green"
      def activeApp = ""

      stage('Blue/Green Production Deployment') {
        // TBD: Determine which application is active
        //      Set Image, Set VERSION
        //      Deploy into the other application
        //      Make sure the application is running and ready before proceeding
        // Deploy the idle application alongside the active application 
        activeApp = sh(returnStdout: true, script: "oc get route openshift-tasks -n ${GUID}-tasks-prod -o jsonpath='{ .spec.to.name }'").trim()
        if (activeApp == "openshift-tasks-green") {
          destApp = "tasks-blue"
          destcfgMap = "Blue"
        }
        echo "Active Application:      " + activeApp
        echo "Destination Application: " + destApp
        echo "Config Map to be used:   " + destcfgMap

        // Update the Image on the Production Deployment Config
        sh "oc set image dc/${destApp} ${destApp}=docker-registry.default.svc:5000/${GUID}-tasks-dev/tasks:${prodTag} -n ${GUID}-tasks-prod"

        // Update the Config Map 
        sh "oc delete configmap openshift-tasks-config -n ${GUID}-tasks-prod --ignore-not-found=true"
        sh "oc create configmap openshift-tasks-config \
                  --from-literal=APPNAME='openshift-tasks (${destcfgMap})' --from-literal=VERSION='${prodTag} (${destApp})' -n ${GUID}-tasks-prod"

        sh "oc set env dc/${destApp} --from=configmap/openshift-tasks-config -n ${GUID}-tasks-prod"

        // Create the service so that the label selector is applied by the refresh of openshift-tasks cache 
        sh "oc expose dc ${destApp} --port 8080 -n ${GUID}-tasks-prod" 

        // Deploy the inactive application
        openshiftDeploy depCfg: destApp, namespace: "${GUID}-tasks-prod", verbose: 'false', waitTime: '', waitUnit: 'sec'
        openshiftVerifyDeployment depCfg: destApp, namespace: "${GUID}-tasks-prod", replicaCount: '1', verbose: 'false', verifyReplicaCount: 'true', waitTime: '', waitUnit: 'sec'
        openshiftVerifyService namespace: "${GUID}-tasks-prod", svcName: destApp, verbose: 'false'        
      }

      stage('Switch over to new Version') {
        echo "Switching Production application to ${destApp}."
        sh "oc patch route openshift-tasks -n ${GUID}-tasks-prod -p '{\"spec\":{\"to\":{\"name\":\"" + destApp + "\"}}}'"
        sh "oc delete svc ${activeApp} -n ${GUID}-tasks-prod"        
      }
    }
  }
}

// Convenience Functions to read version from the pom.xml
// Do not change anything below this line.
// --------------------------------------------------------
def getVersionFromPom(pom) {
  def matcher = readFile(pom) =~ '<version>(.+)</version>'
  matcher ? matcher[0][1] : null
}